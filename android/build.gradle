/*
 * Copyright 2016 Davide Maestroni
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.library'
apply plugin: 'jacoco'
apply plugin: 'findbugs'
apply plugin: 'com.github.ben-manes.versions'
apply from: '../extra.gradle'

group = project.getProperty('groupName')
version = project.getProperty('releaseVersion')
archivesBaseName = "${project.parent.name}-${project.name}"

ext {
  androidChannelVersion = '7.0.0'
  androidCoreVersion = '7.0.0'
  androidMethodVersion = '7.0.0'
  androidProxyVersion = '7.0.0'
  androidReflectVersion = '7.0.0'
  androidStreamVersion = '7.0.0'
  channelVersion = '7.0.0'
  coreVersion = '7.0.0'
  functionVersion = '7.0.0'
  libVersion = '7.0.0'
  reflectVersion = '7.0.0'
  streamVersion = '7.0.0'
}

android {
  compileSdkVersion 23
  buildToolsVersion '23.0.3'

  defaultConfig {
    minSdkVersion 4
    targetSdkVersion 23
    versionCode Integer.parseInt(project.getProperty('releaseVersionCode'))
    versionName version
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }

    debug {
      minifyEnabled false
      testCoverageEnabled true
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_6
    targetCompatibility JavaVersion.VERSION_1_6
  }

  dexOptions {
    preDexLibraries false
  }

  packagingOptions {
    exclude 'META-INF/services/javax.annotation.processing.Processor'
  }

  lintOptions {
    disable 'MissingRegistered'
  }
}

dependencies {
  androidTestCompile 'com.squareup.assertj:assertj-android:1.1.1'
  compile 'com.android.support:support-v4:23.2.1'
}

conditionalDependencies {
  compile name: 'android-channel', version: androidChannelVersion
  compile name: 'android-core', version: androidCoreVersion
  compile name: 'android-method', version: androidMethodVersion
  compile name: 'android-proxy', version: androidProxyVersion
  compile name: 'android-reflect', version: androidReflectVersion
  compile name: 'android-stream', version: androidStreamVersion
  compile name: 'channel', version: channelVersion
  compile name: 'core', version: coreVersion
  compile name: 'function', version: functionVersion
  compile name: 'lib', version: libVersion
  compile name: 'reflect', version: reflectVersion
  compile name: 'stream', version: streamVersion
}

jacoco {
  toolVersion project.getProperty('jacocoVersion')
}

findbugs {
  toolVersion project.getProperty('findbugsVersion')
}

tasks.withType(Javadoc) {
  def prefix = 'http://davide-maestroni.github.io/jroutine/javadoc'
  options.overview("${project.projectDir}/docs/overview.html")
  options.links("${prefix}/${androidChannelVersion}/android-channel/",
      "${prefix}/${androidCoreVersion}/android-core/",
      "${prefix}/${androidMethodVersion}/android-method/",
      "${prefix}/${androidProxyVersion}/android-proxy/",
      "${prefix}/${androidReflectVersion}/android-reflect/",
      "${prefix}/${androidStreamVersion}/android-stream/",
      "${prefix}/${channelVersion}/channel/",
      "${prefix}/${coreVersion}/core/",
      "${prefix}/${functionVersion}/function/",
      "${prefix}/${libVersion}/lib/",
      "${prefix}/${reflectVersion}/reflect/",
      "${prefix}/${streamVersion}/stream/")
}
