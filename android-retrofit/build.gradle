/*
 * Copyright 2016 Davide Maestroni
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.library'
apply plugin: 'jacoco'
apply plugin: 'findbugs'
apply plugin: 'com.github.ben-manes.versions'
apply from: '../extra.gradle'

group = project.getProperty('groupName')
version = project.getProperty('releaseVersion')
archivesBaseName = "${project.parent.name}-${project.name}"

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.3'

    defaultConfig {
        minSdkVersion 4
        targetSdkVersion 23
        versionCode Integer.parseInt(project.getProperty('releaseVersionCode'))
        versionName version
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            testCoverageEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }

    dexOptions {
        preDexLibraries false
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    lintOptions {
        disable 'MissingRegistered'
        lintConfig file('lint.xml')
    }
}

dependencies {
    androidTestCompile 'com.squareup.assertj:assertj-android:1.1.1'
    androidTestCompile 'com.squareup.okhttp3:mockwebserver:3.4.1'
    androidTestCompile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.android.support:support-v4:23.2.1'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
}

conditionalDependencies {
    compile name: 'android-core', version: '6.1.0'
    compile name: 'android-object', version: '6.1.0'
    compile name: 'android-stream', version: '6.1.0'
    compile name: 'core', version: '6.1.0'
    compile name: 'object', version: '6.1.0'
    compile name: 'retrofit', version: '6.1.0'
    compile name: 'stream', version: '6.1.0'
}

jacoco {
    toolVersion project.getProperty('jacocoVersion')
}

findbugs {
    toolVersion project.getProperty('findbugsVersion')
}

tasks.withType(FindBugs) {
    excludeFilter = file('findbugs.xml')
}

tasks.withType(Javadoc) {
    def prefix = 'http://davide-maestroni.github.io/jroutine/javadoc/6'
    options.overview("${project.projectDir}/docs/overview.html")
    options.links('https://square.github.io/okhttp/3.x/okhttp/',
            'https://square.github.io/retrofit/2.x/retrofit/',
            "${prefix}/android-channel/",
            "${prefix}/android-core/",
            "${prefix}/android-object/",
            "${prefix}/android-stream/",
            "${prefix}/channel/",
            "${prefix}/core/",
            "${prefix}/function/",
            "${prefix}/object/",
            "${prefix}/operator/",
            "${prefix}/retrofit/",
            "${prefix}/stream/")
}
