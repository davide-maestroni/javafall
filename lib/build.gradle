/**
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import proguard.gradle.ProGuardTask

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'findbugs'
apply from: '../upload.gradle'

group = 'com.github.davide-maestroni'
version = '1.0.0'
archivesBaseName = project.parent.name

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.google.code.findbugs:annotations:3.0.0'

    testCompile 'junit:junit:4.11'
    testCompile 'org.easytesting:fest-assert-core:2.0M10'
    testCompile project(":jroutineprocessor")
}

task proguardTask(type: ProGuardTask) {
    configuration file("${project.projectDir}/proguard-project.txt")
    injars file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar")
    libraryjars file("${System.env.JAVA_HOME}jre/lib/rt.jar")
    libraryjars configurations.compile.filter { !it.isDirectory() }
    outjars file("${project.buildDir}/libs/${archivesBaseName}-${version}-pro.jar")
}

task('jacocoTest', dependsOn: 'test') {
    description = "Runs the tests and generates the JaCoCo report files."
} << {
    tasks.jacocoTestReport.execute()
}

javadoc {
    title = "${archivesBaseName} ${version}"
    options.links('http://docs.oracle.com/javase/7/docs/api/')
    options.overview("${project.projectDir}/docs/overview.html")
    options.windowTitle("Javadoc - ${archivesBaseName}")
}

findbugs {
    toolVersion = '3.0.0'
    effort = 'max'
    reportLevel = 'low'
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

tasks.findbugsTest.onlyIf { false }

artifacts {
    archives task('sourcesJar', type: Jar) {
        description "Generates sources Jar"
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    archives task('javadocJar', type: Jar, dependsOn: 'javadoc') {
        description "Generates Javadoc Jar"
        classifier = 'javadoc'
        from 'build/docs/javadoc'
    }
}