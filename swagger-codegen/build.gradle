/*
 * Copyright 2016 Davide Maestroni
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'
apply plugin: 'findbugs'
apply plugin: 'com.github.ben-manes.versions'
apply from: '../extra.gradle'

group = project.getProperty('groupName')
version = project.getProperty('releaseVersion')
archivesBaseName = "${project.parent.name}-${project.name}"
compileJavaCompatibility = JavaVersion.VERSION_1_6
compileTestJavaCompatibility = JavaVersion.VERSION_1_7

repositories {
  maven {
    url 'https://oss.sonatype.org/content/repositories/snapshots'
  }
}

configurations.create('swaggerCompile')

ext {
  swaggerVersion = '2.2.2-20170113.000507-4'
}

dependencies {
  compile 'com.intellij:annotations:12.0'
  compile "io.swagger:swagger-codegen-cli:${swaggerVersion}"
  swaggerCompile project
  testCompile project(':operator')
  testCompile project(':retrofit')
  testCompile 'com.squareup.okhttp3:mockwebserver:3.4.1'
  testCompile 'com.squareup.retrofit2:converter-gson:2.1.0'
  testCompile 'com.squareup.retrofit2:converter-scalars:2.1.0'
  testCompile 'com.squareup.retrofit2:converter-jackson:2.1.0'
  testCompile 'com.squareup.retrofit2:retrofit:2.1.0'
  testCompile 'junit:junit:4.12'
  testCompile 'org.apache.oltu.oauth2:org.apache.oltu.oauth2.client:1.0.2'
  testCompile 'org.assertj:assertj-core:2.4.1'
}

ext {
  generatedSourceDir = 'src/test/java/com/github/dm/jrt/swagger/client'
}

clean {
  delete generatedSourceDir
}

findbugs {
  toolVersion project.getProperty('findbugsVersion')
}

javadoc {
  options.overview("${project.projectDir}/docs/overview.html")
}

task generateProperties {
  def outDir = "${buildDir}/generated/properties"
  project.sourceSets.main.resources.srcDir outDir
  doFirst {
    file(outDir).mkdirs()
    def propFile = new File(outDir, 'project.properties')
    propFile << "swaggerVersion=${project.swaggerVersion}\n"
    propFile << "projectVersion=${project.version}\n"
  }
}

task generateSwaggerCode(type: JavaExec, dependsOn: 'jar') {
  main = 'io.swagger.codegen.SwaggerCodegen'
  classpath = project.configurations.swaggerCompile
  args 'generate', '-l', 'jroutine', '-i', 'src/test/resources/swagger.yaml', '-o', "$buildDir/swagger",
      '--invoker-package', 'com.github.dm.jrt.swagger.client',
      '--model-package', 'com.github.dm.jrt.swagger.client.model',
      '--api-package', 'com.github.dm.jrt.swagger.client.api',
      '-DconverterLibrary=jackson'
}

task copySwaggerCode(type: Copy, dependsOn: 'generateSwaggerCode') {
  doFirst {
    project.delete(generatedSourceDir)
  }
  from "$buildDir/swagger/src/main/java"
  into 'src/test/java'
}

compileTestJava.dependsOn copySwaggerCode
compileJava.dependsOn generateProperties