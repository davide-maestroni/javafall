/*
 * Copyright 2016 Davide Maestroni
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.library'
apply plugin: 'jacoco'
apply plugin: 'findbugs'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.github.ben-manes.versions'
apply from: '../extra.gradle'

group = project.getProperty('groupName')
version = project.getProperty('releaseVersion')
archivesBaseName = "${project.parent.name}-${project.name}"

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    defaultConfig {
        minSdkVersion 4
        targetSdkVersion 23
        versionCode Integer.parseInt(project.getProperty('releaseVersionCode'))
        versionName version
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            testCoverageEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }

    dexOptions {
        preDexLibraries false
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

dependencies {
    androidTestCompile 'org.assertj:assertj-core:1.7.1'
    apt project(':androidprocessor')
    compile 'com.android.support:support-v4:23.2.1'
}
conditionalDependencies {
    compile name: 'core', artifact: { "${it.parent.name}-${it.name}" }
    compile name: 'object', artifact: { "${it.parent.name}-${it.name}" }
    compile name: 'proxy', artifact: { "${it.parent.name}-${it.name}" }
    compile name: 'androidcore', artifact: { "${it.parent.name}-${it.name}" }
    compile name: 'androidobject', artifact: { "${it.parent.name}-${it.name}" }
    compile name: 'androidprocessor', artifact: { "${it.parent.name}-${it.name}" }
}

findbugs {
    toolVersion '3.0.1'
}

tasks.withType(Javadoc) {
    def prefix = 'http://davide-maestroni.github.io/jroutine/javadoc/6'
    options.overview("${project.projectDir}/docs/overview.html")
    options.links('http://docs.oracle.com/javase/7/docs/api/',
            "${prefix}/core/",
            "${prefix}/object/",
            "${prefix}/proxy/",
            "${prefix}/androidcore/",
            "${prefix}/androidobject/",
            "${prefix}/androidprocessor/")
}
